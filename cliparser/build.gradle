plugins {
    id 'com.gradleup.shadow' version '9.2.2'
    id "java"
}

compileJava {
    options.compilerArgs << '-parameters'
}

ext {
    javaMainClass = "com.jwoglom.pumpx2.cliparser.Main"
}

shadowJar {
    archiveBaseName.set('cliparser')
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': javaMainClass
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

publishing {
    publications {
        "$project.name"(MavenPublication) {
            artifactId "pumpx2-cliparser"
            groupId rootProject.group
            version rootProject.version
            from components.java
            pom {
                name = "PumpX2 CLIParser"
                description = "A sample CLI tool which uses the PumpX2 Java library to parse and decode t:slim X2 insulin pump message byte streams."
                url = "https://github.com/jwoglom/pumpX2"
                licenses {
                    license {
                        name = "The MIT License (MIT)"
                        url = "http://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "jwoglom"
                        name = "James Woglom"
                        email = "j@wogloms.net"
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:jwoglom/pumpX2.git"
                    developerConnection = "scm:git:git@github.com:jwoglom/pumpX2.git"
                    url = "scm:git:git@github.com:jwoglom/pumpX2.git"
                }
            }
        }
    }
}

// ensure built JAR is executable
tasks.jar {
    manifest.attributes["Main-Class"] = javaMainClass
}


dependencies {
    implementation project(':shared')
    implementation project(':messages')

    implementation "commons-codec:commons-codec:1.15"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "com.google.guava:guava:31.0.1-android"

    implementation 'org.json:json:20180813'
}

task cliparser(type: JavaExec) {
    group = "Execution"
    description = "Run CLIParser"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = javaMainClass
    standardInput(System.in)
}