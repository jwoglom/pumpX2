plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "application"
}
apply plugin : "java"
ext {
    javaMainClass = "com.jwoglom.pumpx2.cliparser.Main"
}

application {
    mainClassName = javaMainClass
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}


publishing {
    publications {
        "$project.name"(MavenPublication) {
            artifactId "pumpx2-cliparser"
            groupId rootProject.group
            version rootProject.version
            from components.java
            pom {
                name = "PumpX2 CLIParser"
                description = "A sample CLI tool which uses the PumpX2 Java library to parse and decode t:slim X2 insulin pump message byte streams."
                url = "https://github.com/jwoglom/pumpX2"
                licenses {
                    license {
                        name = "The MIT License (MIT)"
                        url = "http://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "jwoglom"
                        name = "James Woglom"
                        email = "j@wogloms.net"
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:jwoglom/pumpX2.git"
                    developerConnection = "scm:git:git@github.com:jwoglom/pumpX2.git"
                    url = "scm:git:git@github.com:jwoglom/pumpX2.git"
                }
            }
        }
    }
}

// ensure built JAR is executable
tasks.jar {
    manifest.attributes["Main-Class"] = javaMainClass
}


dependencies {
    implementation project(':shared')
    implementation project(':messages')

    implementation "commons-codec:commons-codec:1.15"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "com.google.guava:guava:31.0.1-android"

    implementation 'org.json:json:20180813'
}

task cliparser(type: JavaExec) {
    group = "Execution"
    description = "Run CLIParser"
    classpath = sourceSets.main.runtimeClasspath
    main = javaMainClass
    standardInput(System.in)
}